buildscript {
    repositories {
        maven { url = 'https://files.minecraftforge.net/maven' }
        maven { url = 'https://repo.spongepowered.org/repository/maven-public/' }
        jcenter()
        mavenCentral()
    }
    dependencies {
        classpath group: 'net.minecraftforge.gradle', name: 'ForgeGradle', version: '5.1.+', changing: true
        classpath 'org.spongepowered:mixingradle:0.7-SNAPSHOT'
    }
}
apply plugin: 'net.minecraftforge.gradle'
apply plugin: 'eclipse'
apply plugin: 'maven-publish'
apply plugin: 'org.spongepowered.mixin'

version = "${System.env.VERSION}"
group = 'snownee.jade'
archivesBaseName = 'amber'

sourceCompatibility = targetCompatibility = compileJava.sourceCompatibility = compileJava.targetCompatibility = '1.8' // Need this here so eclipse task generates correctly.

minecraft {
    mappings channel: 'official', version: '1.16.5'

    accessTransformer = file('src/main/resources/META-INF/accesstransformer.cfg')

    runs {
        client {
            workingDirectory project.file('run')

            property 'forge.logging.markers', ''
            property 'forge.logging.console.level', 'debug'

            mods {
                amber {
                    source sourceSets.main
                }
            }
        }

        server {
            workingDirectory project.file('run')

            property 'forge.logging.markers', ''
            property 'forge.logging.console.level', 'debug'

            mods {
                amber {
                    source sourceSets.main
                }
            }
        }
    }
}

task apiJar(type: Jar) {
    classifier = 'api'
    include 'mcp/mobius/waila/api/**/*'
    exclude 'mcp/mobius/waila/api/impl/**/*'
    from sourceSets.main.allSource
    from sourceSets.main.output
}

task deobfJar(type: Jar) {
    from(sourceSets.main.output)
    archiveName = "${archivesBaseName}-${version}-deobf.${extension}"
}

repositories {
    maven {
        name = "jei"
        url = "https://dvs1.progwml6.com/files/maven"
    }
    maven {
        url = "https://www.cursemaven.com"
    }
    maven {
        name "Modmaven"
        url "https://modmaven.dev/"
    }
}

dependencies {
    minecraft 'net.minecraftforge:forge:1.16.5-36.2.34'
    annotationProcessor 'org.spongepowered:mixin:0.8.5:processor'

    compileOnly fg.deobf("mezz.jei:jei-1.16.5:7.7.1.153:api")
    runtimeOnly fg.deobf("mezz.jei:jei-1.16.5:7.7.1.153")

    implementation fg.deobf("curse.maven:lollipop-347954:3232534")
    implementation fg.deobf("curse.maven:powah-352656:3232535")

    implementation fg.deobf("curse.maven:mcjtylib-233105:3848282")
    implementation fg.deobf("curse.maven:rftoolsbase-326041:3825685")
    implementation fg.deobf("curse.maven:rftoolspower-290209:3783989")

    implementation fg.deobf("curse.maven:titanium-287342:3525770")
    implementation fg.deobf("curse.maven:if-266515:3525789")
    implementation fg.deobf("curse.maven:ie-231951:3809596")

    implementation fg.deobf("curse.maven:mekanism-268560:3659389")
    implementation fg.deobf("appeng:appliedenergistics2:8.4.7")

    implementation fg.deobf("curse.maven:flywheel-486392:3535459")
    implementation fg.deobf("curse.maven:create-328085:3536025")
    
    implementation fg.deobf("curse.maven:farmersdelight-398521:3765350")
    implementation fg.deobf("curse.maven:dynamictrees-252818:4457951")
}

jar {
    manifest {
        attributes([
            "Implementation-Title": project.name,
            "Implementation-Version": "${version}",
            "Implementation-Timestamp": new Date().format("yyyy-MM-dd'T'HH:mm:ssZ")
        ])
    }
}

def reobfFile = file("$buildDir/reobfJar/output.jar")
def reobfArtifact = artifacts.add('default', reobfFile) {
    type 'jar'
    builtBy 'reobfJar'
}
publishing {
    publications {
        mavenJava(MavenPublication) {
            artifact reobfArtifact
        }
    }
    repositories {
        maven {
            url "file:///${project.projectDir}/mcmodsrepo"
        }
    }
}